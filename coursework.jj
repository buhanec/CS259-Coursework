options {
    LOOKAHEAD = 2;
    FORCE_LA_CHECK = true;
}

PARSER_BEGIN(CS259Parser)
import java.util.*;

public class CS259Parser {
    public static void main(String[] args) throws EvalException {
        CS259Parser parser = new CS259Parser(System.in);
        try {
            parser.Program();
        } catch (Exception e) {
            e.printStackTrace();
        }

        for (String n: Functions.map.keySet()) {
            String fn = Functions.map.get(n).toString();
            System.out.println(fn);
        }

        System.out.println(Functions.map.get("MAIN").eval(new int[0]));

    }
}

/**
 * Exception raised during evaluation.
 */
class EvalException extends Exception {
    public EvalException() {
        super();
    }
    public EvalException(String message) {
        super(message);
    }
}

/**
 * Interface that needs to be implemented by all the elements of an expression.
 */
interface Evalable {
    public int eval(Map<String, Integer> args) throws EvalException;
    public int getLine();
    public int getColumn();
}

class Positioned {
    public final int line;
    public final int column;

    public Positioned(Token t) {
        this.line = t.beginLine;
        this.column = t.beginColumn;
    }
    public Positioned(int line, int column) {
        this.line = line;
        this.column = column;
    }

    public int getLine() {
        return line;
    }

    public int getColumn() {
        return column;
    }
}

/**
 * Represents a function that can be called with an array of numeric arguments.
 */
class Function extends Positioned {
    public final String name;
    public final String[] args;
    public final Evalable expr;

    public Function(String name, String[] args, Evalable expr, int line) {
        super(line, 0);
        this.name = name;
        this.args = args;
        this.expr = expr;
    }

    public int eval(int[] args) throws EvalException {
        if (args.length != this.args.length) {
            throw new EvalException("Incorrect number of arguments.");
        }
        Map<String, Integer> nargs = new HashMap<String, Integer>();
        for (int i = 0; i < args.length; i++) {
            nargs.put(this.args[i], args[i]);
        }
        return expr.eval(nargs);
    }

    public String toString() {
        String r = name+"(";
        for (int i = 0; i < args.length; i++) {
            if (i == args.length-1) {
                r += args[i];
            } else {
                r += args[i]+",";
            }
        }
        return r+"):="+expr;
    }
}

/**
 * Represents an (unsigned) integer.
 */
class Number extends Positioned implements Evalable {
    public final int value;

    public Number(Token t) {
        super(t);
        this.value = Integer.parseInt(t.image);
    }
    public Number(String value, int line, int column) {
        super(line, column);
        this.value = Integer.parseInt(value);
    }
    public Number(int value, int line, int column) {
        super(line, column);
        this.value = value;
    }

    public int eval(Map<String, Integer> args) {
        return value;
    }

    public String toString() {
        return Integer.toString(value);
    }
}

/**
 * Represents a variable/argument.
 */
class Argument extends Positioned implements Evalable {
    public final String name;

    public Argument(Token t) {
        super(t);
        this.name = t.image;
    }
    public Argument(String name, int line, int column) {
        super(line, column);
        this.name = name;
    }

    public int eval(Map<String, Integer> args) {
        return args.get(name);
    }

    public String toString() {
        return name;
    }
}

/**
 * Represents a call to a function.
 */
class Call extends Positioned implements Evalable {
    public final String function;
    public final Evalable[] args;

    public Call(Token t, Evalable[] args) {
        super(t);
        this.function = t.image;
        this.args = args;
    }
    public Call(String function, Evalable[] args, int line, int column) {
        super(line, column);
        this.function = function;
        this.args = args;
    }

    public int eval(Map<String, Integer> args) throws EvalException {
        int retval;
        if (Functions.stack.search(this) > -1) {
            throw new EvalException("Recursion");
        }
        Functions.stack.push(this);
        int[] iargs = new int[this.args.length];
        for (int i = 0; i < iargs.length; i++) {
            iargs[i] = this.args[i].eval(args);
        }
        retval = Functions.map.get(function).eval(iargs);
        Functions.stack.pop();
        return retval;
    }

    public String toString() {
        String r = function+"(";
        for (int i = 0; i < args.length; i++) {
            if (i == args.length-1) {
                r += args[i];
            } else {
                r += args[i]+",";
            }
        }
        return r+")";
    }
}

/**
 * Represents a product.
 */
class Product extends Positioned implements Evalable {
    public final Evalable factor1;
    public final Evalable factor2;

    public Product(Evalable factor1, Evalable factor2, int line, int column) {
        super(line, column);
        this.factor1 = factor1;
        this.factor2 = factor2;
    }

    public int eval(Map<String, Integer> args) throws EvalException {
        return factor1.eval(args)*factor2.eval(args);
    }

    public String toString() {
        return "("+factor1+"*"+factor2+")";
    }
}

/**
 * Represents a sum.
 */
class Sum extends Positioned implements Evalable {
    public final Evalable summand1;
    public final Evalable summand2;

    public Sum(Evalable summand1, Evalable summand2, int line, int column) {
        super(line, column);
        this.summand1 = summand1;
        this.summand2 = summand2;
    }

    public int eval(Map<String, Integer> args) throws EvalException {
        return summand1.eval(args)+summand2.eval(args);
    }

    public String toString() {
        return "("+summand1+"+"+summand2+")";
    }
}

/**
 * Class that stores data required for evaluation.
 */
class Functions {
    public static Map<String, Function> map = new HashMap<String, Function>();
    public static Set<Call> calls = new HashSet<Call>();
    public static Stack<Call> stack = new Stack<Call>();
}

PARSER_END(CS259Parser)

/**
 * Tokens
 */
TOKEN :
{
        < PLUS  : "+" >
    |   < TIMES : "*" >
    |   < LPAR  : "(" >
    |   < RPAR  : ")" >
    |   < LANG  : "{" >
    |   < RANG  : "}" >
    |   < S     : " " >
    |   < DEF   : "DEF" >
    |   < MAIN  : "MAIN" >
    |   < EOL   : ";" ( "\r" | "\n" | "\r\n" ) >
    |   < FUNC  : ( <UPPER> )+ >
    |   < ARG   : ( <LOWER> )+ >
    |   < NUM   : ( <DIGIT> )+ >
}
TOKEN :
{
        < #DIGIT : ["0"-"9"] >
    |   < #UPPER : ["A"-"Z"] >
    |   < #LOWER : ["a"-"z"] >
}

void Program() : { Function f; int i = 1; }
{
    ( f = Def(i++) { Functions.map.put(f.name, f); }
    )* <EOF> {
        if (!Functions.map.containsKey("MAIN")) {
            throw new ParseException("No MAIN function.");
        }
        for (Call c : Functions.calls) {
            if (!Functions.map.containsKey(c.function) ||
                Functions.map.get(c.function).args.length != c.args.length) {
                throw new ParseException("Call to non-existent function \"" +
                                         c + "\" at line " + c.line +
                                         ", column " + c.column + ".");
            }
        }
    }
}

Function Def(int i) : {
    Token t;
    Evalable e;
    List<String> l = new ArrayList<String>();
    Set<String> s = new HashSet<String>();
}
{
    <DEF> <S>
    (
            ( t = <MAIN> )
        |   ( t = <FUNC> (
                <S> <ARG> { l.add(token.image); } l = Arguments(l) )?
            )
    ) { s.addAll(l); }
    <S> <LANG> <S> e = Sums(s) <S> <RANG> <S> <EOL>
    {
        return new Function(t.image, l.toArray(new String[l.size()]), e, i);
    }
}

List<String> Arguments(List<String> s) : { Token t; }
{
    ( <S> t = <ARG> { s.add(t.image); } )* { return s; }
}

List<Evalable> CallArguments(Set<String> s, List<Evalable> l) : {
    Evalable e;
}
{
    ( <S> e = Sums(s) { l.add(e); } )* { return l; }
}

Evalable Nullnary(Set<String> s) : {
    Token t;
    Evalable e;
    List<Evalable> l = new ArrayList<Evalable>();
}
{
        <NUM> { return new Number(token); }
    |   <ARG> {
            if (!s.contains(token.image)) {
                throw new ParseException("Incorrect argument \"" +
                                         token.image + " at line " +
                                         token.beginLine + ", column " +
                                         token.beginColumn + ".");
            }
            return new Argument(token);
        }
    |   t = <FUNC> <LPAR>
        ( e = Sums(s) { l.add(e); } l = CallArguments(s, l) )? <RPAR> {
            Call c = new Call(t, l.toArray(new Evalable[l.size()]));
            Functions.calls.add(c);
            return c;
        }
}

/**
 * Initialises the matching of a product in the language. If no product can be
 * created an expression of the closest existing higher precedence is returned.
 *
 * @param  s the set of all valid parameter names in the expression
 * @return   the product or higher precedence expression
 */
Evalable Products(Set<String> s) : { Evalable e; }
{
    e = Nullnary(s) e = CreateProduct(s, e) { return e; }
}

/**
 * Performs the matching of a product in the language.
 *
 * @param  s the set of all valid parameter names in the expression.
 * @param  p the partal product so far
 * @return   the final product
 */
Evalable CreateProduct(Set<String> s, Evalable p) : { Evalable e; }
{
    ( <TIMES> e = Nullnary(s) {
        p = new Product(p, e, p.getLine(), p.getColumn());
    } )* { return p; }
}

/**
 * Initialises the matching of a sum in the language. If no sum can be created
 * an expression of the closest existing higher precedence is returned.
 *
 * @param  s the set of all valid parameter names in the expression
 * @return   the sum or higher precedence expression
 */
Evalable Sums(Set<String> s) : { Evalable e; }
{
    e = Products(s) e = CreateSum(s, e) { return e; }
}

/**
 * Performs the matching of a sum in the language.
 *
 * @param  s the set of all valid parameter names in the expression.
 * @param  p the partal sum so far
 * @return   the final sum
 */
Evalable CreateSum(Set<String> s, Evalable p) : { Evalable e; }
{
    ( <PLUS> e = Products(s) {
        p = new Sum(p, e, p.getLine(), p.getColumn());
    } )* { return p; }
}
