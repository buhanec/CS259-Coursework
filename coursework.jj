options {
    LOOKAHEAD = 2;
    FORCE_LA_CHECK = true;
}

PARSER_BEGIN(CS259Parser)
import java.util.*;

public class CS259Parser {
    public static void main(String[] args) throws EvalException {
        CS259Parser parser = new CS259Parser(System.in);
        try {
            parser.Program();
        } catch (Exception e) {
            e.printStackTrace();
        }

        for (String n: Functions.map.keySet()) {
            String fn = Functions.map.get(n).toString();
            System.out.println(fn);
        }

        System.out.println(Functions.map.get("MAIN").eval(new int[0]));

    }
}

/**
 * Exception raised during evaluation.
 */
class EvalException extends Exception {
    public EvalException() {
        super();
    }
    public EvalException(String message) {
        super(message);
    }
}

/**
 * Interface that needs to be implemented by all the elements of an expression.
 */
interface Evalable {
    public int eval(Map<String, Integer> args) throws EvalException;
}

/**
 * Represents a function that can be called with an array of numeric arguments.
 */
class Function {
    public final String name;
    public final String[] args;
    public final Evalable expr;

    public Function(String name, String[] args, Evalable expr) {
        this.name = name;
        this.args = args;
        this.expr = expr;
    }

    public int eval(int[] args) throws EvalException {
        if (args.length != this.args.length) {
            throw new EvalException("Incorrect number of arguments.");
        }
        Map<String, Integer> nargs = new HashMap<String, Integer>();
        for (int i = 0; i < args.length; i++) {
            nargs.put(this.args[i], args[i]);
        }
        return expr.eval(nargs);
    }

    public String toString() {
        String r = name+"(";
        for (int i = 0; i < args.length; i++) {
            if (i == args.length-1) {
                r += args[i];
            } else {
                r += args[i]+",";
            }
        }
        return r+"):="+expr;
    }
}

/**
 * Represents an (unsigned) integer.
 */
class Number implements Evalable {
    public final int value;

    public Number(String value) {
        this.value = Integer.parseInt(value);
    }
    public Number(int value) {
        this.value = value;
    }

    public int eval(Map<String, Integer> args) {
        return value;
    }

    public String toString() {
        return Integer.toString(value);
    }
}

/**
 * Represents a variable/argument.
 */
class Argument implements Evalable {
    public final String name;

    public Argument(String name) {
        this.name = name;
    }

    public int eval(Map<String, Integer> args) {
        return args.get(name);
    }

    public String toString() {
        return name;
    }
}

/**
 * Represents a call to a function.
 */
class Call implements Evalable {
    public final String function;
    public final Evalable[] args;

    public Call(String function, Evalable[] args) {
        this.function = function;
        this.args = args;
    }

    public int eval(Map<String, Integer> args) throws EvalException {
        int retval;
        if (Functions.stack.search(this) > -1) {
            throw new EvalException("Recursion");
        }
        Functions.stack.push(this);
        int[] iargs = new int[this.args.length];
        for (int i = 0; i < iargs.length; i++) {
            iargs[i] = this.args[i].eval(args);
        }
        retval = Functions.map.get(function).eval(iargs);
        Functions.stack.pop();
        return retval;
    }

    public String toString() {
        String r = function+"(";
        for (int i = 0; i < args.length; i++) {
            if (i == args.length-1) {
                r += args[i];
            } else {
                r += args[i]+",";
            }
        }
        return r+")";
    }
}

/**
 * Represents a product.
 */
class Product implements Evalable {
    public final Evalable factor1;
    public final Evalable factor2;

    public Product(Evalable factor1, Evalable factor2) {
        this.factor1 = factor1;
        this.factor2 = factor2;
    }

    public int eval(Map<String, Integer> args) throws EvalException {
        return factor1.eval(args)*factor2.eval(args);
    }

    public String toString() {
        return "("+factor1+"*"+factor2+")";
    }
}

/**
 * Represents a sum.
 */
class Sum implements Evalable {
    public final Evalable summand1;
    public final Evalable summand2;

    public Sum(Evalable summand1, Evalable summand2) {
        this.summand1 = summand1;
        this.summand2 = summand2;
    }

    public int eval(Map<String, Integer> args) throws EvalException {
        return summand1.eval(args)+summand2.eval(args);
    }

    public String toString() {
        return "("+summand1+"+"+summand2+")";
    }
}

/**
 * Class that stores data required for evaluation.
 */
class Functions {
    public static Map<String, Function> map = new HashMap<String, Function>();
    public static Stack<Call> stack = new Stack<Call>();
}

PARSER_END(CS259Parser)

/**
 * Tokens
 */
TOKEN :
{
        < PLUS  : "+" >
    |   < TIMES : "*" >
    |   < LPAR  : "(" >
    |   < RPAR  : ")" >
    |   < LANG  : "{" >
    |   < RANG  : "}" >
    |   < S     : " " >
    |   < DEF   : "DEF" >
    |   < MAIN  : "MAIN" >
    |   < EOL   : ";" ( "\r" | "\n" | "\r\n" ) >
    |   < FUNC  : ( <UPPER> )+ >
    |   < ARG   : ( <LOWER> )+ >
    |   < NUM   : ( <DIGIT> )+ >
}
TOKEN :
{
        < #DIGIT : ["0"-"9"] >
    |   < #UPPER : ["A"-"Z"] >
    |   < #LOWER : ["a"-"z"] >
}

void Program() : { Function f; }
{
    ( f = Def() { Functions.map.put(f.name, f); }
    )* <EOF> {}
}

Function Def() : {
    Token t;
    Evalable e;
    List<String> l = new ArrayList<String>();
    Set<String> s = new HashSet<String>();
}
{
    <DEF> <S>
    (
            ( t = <MAIN> )
        |   ( t = <FUNC> (
                <S> <ARG> { l.add(token.image); } l = Arguments(l) )?
            )
    ) { s.addAll(l); }
    <S> <LANG> <S> e = Sums(s) <S> <RANG> <S> <EOL>
    {
        return new Function(t.image, l.toArray(new String[l.size()]), e);
    }
}

List<String> Arguments(List<String> s) : { Token t; }
{
    ( <S> t = <ARG> { s.add(t.image); } )* { return s; }
}

List<Evalable> CallArguments(Set<String> s, List<Evalable> l) : {
    Evalable e;
}
{
    ( <S> e = Sums(s) { l.add(e); } )* { return l; }
}

Evalable Nullnary(Set<String> s) : {
    Token t;
    Evalable e;
    List<Evalable> l = new ArrayList<Evalable>();
}
{
        <NUM> { return new Number(token.image); }
    |   <ARG> {
            if (!s.contains(token.image)) {
                throw new ParseException("Incorrect argument \"" +
                                         token.image + " at line " +
                                         token.beginLine + ", column " +
                                         token.beginColumn + ".");
            }
            return new Argument(token.image);
        }
    |   t = <FUNC> <LPAR>
        ( e = Sums(s) { l.add(e); } l = CallArguments(s, l) )? <RPAR> {
            return new Call(t.image, l.toArray(new Evalable[l.size()]));
        }
}

Evalable CreateProduct(Set<String> s, Evalable p) : { Evalable e; }
{
    ( <TIMES> e = Nullnary(s) { p = new Product(p, e); } )* { return p; }
}

Evalable Products(Set<String> s) : { Evalable e; }
{
    e = Nullnary(s) e = CreateProduct(s, e) { return e; }
}

Evalable CreateSum(Set<String> s, Evalable p) : { Evalable e; }
{
    ( <PLUS> e = Products(s) { p = new Sum(p, e); } )* { return p; }
}

Evalable Sums(Set<String> s) : { Evalable e; }
{
    e = Products(s) e = CreateSum(s, e) { return e; }
}
